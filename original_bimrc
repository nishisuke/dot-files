augroup myTemplate
	autocmd!
	autocmd BufNewFile *.rb call SetClass()
augroup END

fun SetClass()
  let s:path = expand('%')
  if match(s:path, 'app/') != 0
    return 0
  endif
  let s:path = strcharpart(s:path, 4)
  let s:list = matchlist(s:path, '\v([0-9a-z_]+)/([0-9a-z_/]+)\.rb')

  if s:list[1] == 'views'
    return 0
  elseif s:list[1] == 'models'
    let s:superclass = 'ApplicationRecord'
  elseif s:list[1] == 'controllers'
    let s:superclass = 'ApplicationController'
  else
    let s:superclass = ''
  endif

  let s:class = join(map(split(s:list[2], '/'), function('PascalCase')), '::')

  call setline('.', '# frozen_string_literal: true')
  if strchars(s:superclass) == 0
    call append(1, ['', 'class '.s:class, 'end'])
  else
    call append(1, ['', 'class '.s:class.' < '.s:superclass, 'end'])
  endif
endfun

func PascalCase(key, val)
  return join(map(split(a:val, '_'), function('FirstUpper')), '')
endfunc

func FirstUpper(key, val)
  return substitute(a:val, '^\l', '\u&', '')
endfunc
